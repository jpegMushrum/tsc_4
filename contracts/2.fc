{-
  TASK 2 - Matrix multiplier
  Write the method that multiplies one matrix by another.
  The 1st and 2nd parameter of the function will contain
  a matrix implemented using tuples (eg: [[1,2], [3,4]]).
  Matrices can be of different sizes, ranging from 4x4
  up to 32x32.
  Matrix can be rectangular where matrixA must to be of size n*m & matrixB of size m*p.
  After calculating the result matrix, the function should return final result (as tuple).
-}
(int) tlen (tuple t) asm "TLEN";
forall X -> (tuple) to_tuple (X x) asm "NOP";

() recv_internal() {
}

;; testable
(tuple) matrix_multiplier(tuple matrixA, tuple matrixB) method_id {

  int n = matrixA.tlen();
  int m = matrixB.tlen();
  int p = matrixB.at(0).tlen();

  int i = 0;
  int j = 0;
  int t = 0;

  int item = 0;

  tuple result = empty_tuple();
  tuple stroke = empty_tuple();
  repeat(n) {
    repeat(p) {
      repeat(m) {
        item += matrixA.at(i).at(t) * matrixB.at(t).at(j);
        t += 1;
      }
      stroke~tpush(item);
      j += 1;
      t = 0;
      item = 0;
    }
    result~tpush(stroke);
    stroke = empty_tuple();
    i += 1;
    j = 0;
  }

  return result;
}
